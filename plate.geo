SetFactory("OpenCASCADE");

// Параметры геометрии
l1 = 1.45;        // Длина до пластины
l2 = 0.1;        // Ширина пластины
l3 = 4.0 - l2 - l1; // Длина после пластины
H  = 1.0;       // Высота канала
h_plate = 0.5;   // Высота вертикальной пластины
d = 0.01;        // Характерный размер сетки

// Общая длина
L = l1 + l2 + l3;

// Точки канала
Point(1) = {0, 0, 0, d};            // Нижний левый угол
Point(2) = {L, 0, 0, d};            // Нижний правый угол
Point(3) = {L, H, 0, d};            // Верхний правый угол
Point(4) = {0, H, 0, d};            // Верхний левый угол

// Точки пластины
Point(5) = {l1, 0, 0, d};
Point(6) = {l1 + l2, 0, 0, d};
Point(7) = {l1 + l2, h_plate, 0, d};
Point(8) = {l1, h_plate, 0, d};

// Линии (нижняя грань с пластиной)
Line(1) = {1, 5};        // нижняя левая до пластины
Line(2) = {5, 8};        // левая сторона пластины
Line(3) = {8, 7};        // верхняя сторона пластины
Line(4) = {7, 6};        // правая сторона пластины
Line(5) = {6, 2};        // нижняя правая после пластины
Line(6) = {2, 3};        // правая стенка
Line(7) = {3, 4};        // верхняя стенка
Line(8) = {4, 1};        // левая стенка

// Контур и поверхность
Line Loop(1) = {1, 2, 3, 4, 5, 6, 7, 8};
Plane Surface(1) = {1};

// Физические группы для FEniCS
Physical Surface("fluid") = {1};

Physical Curve("inlet")  = {8};
Physical Curve("outlet") = {6};            
Physical Curve("top") = {7};
Physical Curve("bottom")  = {1, 2, 3, 4, 5};   

Mesh.CharacteristicLengthMin = 0.1;
Mesh.CharacteristicLengthMax = 0.25;
